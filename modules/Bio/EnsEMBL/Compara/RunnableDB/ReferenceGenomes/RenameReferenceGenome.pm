=head1 LICENSE

See the NOTICE file distributed with this work for additional information
regarding copyright ownership.

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

     http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.

=head1 NAME

Bio::EnsEMBL::Compara::RunnableDB::ReferenceGenomes::RenameReferenceGenome

=head1 DESCRIPTION

Renames genome in compara (reference) database as well as in the division's config files and genome dumps.
Files that will stop working due to the renaming and cannot be modified, i.e. exonerate files, are deleted so
they are properly regenerated by their corresponding pipeline.

=over

=item compara_db

Mandatory. Alias of the compara (reference) database.

=item old_name

Mandatory. Old genome (species) name.

=item new_name

Mandatory. New genome (species) name.

=item xml_file

Mandatory. Path to the MLSS configuration XML file.

=item members_dumps_dir

Mandatory. Path to the root folder where the dumps for this genome are stored.

=item allowed_species_file

Optional. Path to the allowed species JSON file.

=back

=head1 EXAMPLES

    standaloneJob.pl Bio::EnsEMBL::Compara::RunnableDB::ReferenceGenomes::RenameReferenceGenome \
        -compara_db compara_references -old_name canis_familiaris -new_name perricus_bonicus \
        -xml_file $ENSEMBL_CVS_ROOT_DIR/ensembl-compara/conf/citest/mlss_conf.xml \
        -genome_dumps_dir /hps/nobackup2/production/ensembl/jalvarez/genome_dumps/ \
        -allowed_species_file $ENSEMBL_CVS_ROOT_DIR/ensembl-compara/conf/citest/allowed_species.json

=cut

package Bio::EnsEMBL::Compara::RunnableDB::ReferenceGenomes::RenameReferenceGenome;

use warnings;
use strict;

use Bio::EnsEMBL::Compara::Utils::ReferenceDatabase;

use base ('Bio::EnsEMBL::Compara::RunnableDB::BaseRunnable');


sub run {
    my $self = shift;

    my $old_name = $self->param_required('old_name');
    my $new_name = $self->param_required('new_name');
    my @config_files = ($self->param_required('xml_file'), $self->param('allowed_species_file'));
    my $members_dumps_dir = $self->param_required('members_dumps_dir');

    my $compara_dba = $self->compara_dba();
    Bio::EnsEMBL::Compara::Utils::ReferenceDatabase::rename_reference_genome($compara_dba, $old_name, $new_name);

    # Rename the reference genome in the configuration files
    foreach my $file (@config_files) {
        next unless ($file && -e $file);
        my $content = $self->_slurp($file);
        $content =~ s/"$old_name"/"$new_name"/;
        $self->_spurt($file, $content);
        print "Updated content of $file\n" if $self->debug;
    }

    # Rename all files and folders for this reference genome
    # NOTE: when the genome has just been renamed the API still needs the old name to return the GenomeDB
    my $genome_db = $compara_dba->get_GenomeDBAdaptor()->fetch_by_name_assembly($old_name);
    $genome_db = $compara_dba->get_GenomeDBAdaptor()->fetch_by_name_assembly($new_name) if (!$genome_db);
    my $fasta_file = $genome_db->_get_members_dump_path($members_dumps_dir);
    $fasta_file =~ s/\s+/_/g;    # replace whitespace with '_' characters
    $fasta_file =~ s/\/\//\//g;  # converts any // in path to /
    $fasta_file =~ s/$new_name/$old_name/;  # in case this is not the first run of the job
    (my $path_glob = $fasta_file) =~ s/.fasta$//;

    my @files_to_rename = glob qq(${path_glob}.split/* ${path_glob}.*);
    foreach my $file (@files_to_rename) {
        (my $new_fname = $file) =~ s/^(.*\/)$old_name/$1$new_name/;  # only rename the file name
        rename $file, $new_fname;
    }
    print "Files renamed:\n", join("\n", @files_to_rename), "\n" if $self->debug;
}


1;
