#!/usr/bin/env perl

# Copyright [1999-2015] Wellcome Trust Sanger Institute and the EMBL-European Bioinformatics Institute
# Copyright [2016-2022] EMBL-European Bioinformatics Institute
# 
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
# 
#      http://www.apache.org/licenses/LICENSE-2.0
# 
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
# helper for ctrl scripts

use strict;
use warnings;

use FindBin qw($Bin);
use Getopt::Long;

BEGIN {
  require "$Bin/helper.pl";
};

# Get some configs from SiteDefs
my $web_root          = $SiteDefs::ENSEMBL_WEBROOT;
my $conf_dir          = $SiteDefs::ENSEMBL_SYS_DIR.'/conf';
my $conf_file         = $SiteDefs::ENSEMBL_CONFIG_FILENAME;
my $memcached         = $SiteDefs::ENSEMBL_MEMCACHED;
my $apache_define     = $SiteDefs::APACHE_DEFINE;
my $apache_dir        = $SiteDefs::APACHE_DIR;
my $apache_bin        = $SiteDefs::APACHE_BIN;
my $apache_conf       = $SiteDefs::ENSEMBL_HTTPD_CONFIG_FILE;
my $log_dir           = $SiteDefs::ENSEMBL_LOGDIR;
my $dedicated_biomart = $SiteDefs::ENSEMBL_MART_SERVERNAME ? 1 : 0; # does it have a deicated biomart server somewhere else?

my ($help, $rmconfig, $flush, $verbose, @defines);

# parse command line options
&GetOptions(
  "r|rmconfig"  => \$rmconfig,
  "f|flush"     => \$flush,
  "v|verbose"   => \$verbose,
  "h|help"      => \$help,
  "D|define=s"  => \@defines
);

if ($help) {
  print qq(
  Usage:
    ctrl_scripts/start_server

  Options:
    -r, --rmconfig  : remove $conf_file before restarting the server and flush memcached
    -f, --flush     : flush memcached storage!
    -v, --verbose   : verbose the SiteDefs params value
    -D, --define    : list of arguments to be passed to apache httpd process
    -h, --help      : print this help message
);

  exit;
}

warn_line();
warn_string('start_server: Starting server');

# Remove preload log
unlink("$log_dir/preload-errors.log");

# Create directory for packed files if it doesn't exist
if (! -e "$conf_dir/packed") {
  warn_string("Creating directory for packed files at $conf_dir/packed");
  system("mkdir -p $conf_dir/packed");
}

# If rebuilding the config file
if ($rmconfig) {
  warn_line();
  warn_string("Removing $conf_file");

  my $ok = unlink "$conf_dir/$conf_file";

  if ($ok) {
    warn_string("Removed $conf_file");
  } elsif (-e "$conf_dir/$conf_file") {
    warn_string("Could not delete $conf_dir/$conf_file: $!\n[WARN] Starting with old $conf_file!");
  }
}

# Additional processing for memcached
if (keys %{$memcached || {}}) {

  # Check memcached servers version
  warn_line();
  warn_string("Checking memcached compatibility...");

  my $err = '';
  my $res = run_script("$web_root/ctrl_scripts/memcached", ['version'], \$err);
  warn $err if $err;

  if ($res >> 8 > 0) {
    warn_string('failed');
    die_string('Please check www.ensembl.org/info/docs/webcode/mirror/memcached');
  }

  if ($flush || $rmconfig) {
    # Flush memcached
    run_script($web_root, ['flush']);
    warn_string("Flushed memcached entries");
  }
}

warn_line();

my ($before_start, $after_start) = before_after_hooks('start');

# Build the httpd command string and run all commands
my $cmd = sprintf '%s -d %s -f %s %s', $apache_bin || "$apache_dir/bin/httpd", $apache_dir, $apache_conf, join(' ', map("-D$_", @defines, $apache_define ? grep(/\S/, split ' ', $apache_define) : ()));
   $cmd = "$cmd -D STARTUP_VERBOSE" if $verbose;
   $cmd = "$cmd -D HAS_DEDICATED_MART" if $dedicated_biomart;

require $_ for @$before_start;
warn "========================\nStarting Apache: $cmd\n========================\n";
system($cmd) and die_string("Could not start server: $!.\n[FATAL] Server start failed.");
require $_ for @$after_start;
