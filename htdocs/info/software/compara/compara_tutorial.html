<html>
<head>
<meta name="author" content="compara" />
<meta name="navigation" content="Ensembl Compara" />
<title>Perl API Tutorial</title>
</head>

<body>

<h2>Perl API Tutorial</h2>


<h3 class="boxed">Introduction</h3>

<p>
This tutorial is an introduction to the
<a href="./index.html">Ensembl Compara API</a>.
Knowledge of the 
<a href="../core/index.html">Ensembl Core API</a> and of the concepts and conventions in the
<a href="../core/core_tutorial.html">Ensembl Core API tutorial</a> is assumed.
<!--
This tutarial should be read first as it provides a comprehensive guide to the Ensembl environment.
-->
Documentation about the Compara database schema is available at
<a href="http://cvsweb.sanger.ac.uk/cgi-bin/cvsweb.cgi/ensembl-compara/docs/">
http://cvsweb.sanger.ac.uk/cgi-bin/cvsweb.cgi/ensembl-compara/docs/</a> 
(or in ensembl-compara/docs/docs/schema_doc.html from the Ensembl CVS repository), and while not necessary for this tutorial,
an understanding of the database tables may help as many of the adaptor modules are table specific.
</p>


<h3 class="boxed">Obtaining the code</h3>

<p>
The following are required to use Ensembl compara API (the same list as for the Ensembl core API): 
</p>

<ul>
<li>perl 5.6 or later</li>
<li>bioperl 1.2 or later</li>
<li>DBI, DBD::mysql</li>
<li>Ensembl core code.
</li>
</ul>
<p>

Please refer to the Ensembl core API tutorial for information on these modules, how and where to get them.
Start by creating a directory for storing the API in your home directory:
</p>

<div class="example">
<pre>
cd
mkdir src
cd src
</pre>
</div>

<p>
In addition, you will need the ensembl compara code that is available by cvs from the ensembl cvs repository using the following cvs commands:
</p>

<div class="example">
<pre>
cvs -d :pserver:cvsuser@cvs.sanger.ac.uk:/cvsroot/ensembl login
</pre>
</div>

<p>
When prompted the password is &quot;CVSUSER&quot;.
</p>

<div class="example">
<pre>
cvs -d :pserver:cvsuser@cvs.sanger.ac.uk:/cvsroot/ensembl co -r branch-ensembl-45 ensembl-compara
</pre>
</div>

<p>
This will check out ensembl-compara code for stable branch 45.
Make sure the ensembl core code you have already checked out is on the same branch.
Note that the branch that is checked out should correspond to the database <a href="/info/software/versions.html">version</a> being used.
Thus ensembl_compara_45 and e.g. homo_sapiens_core_45_36g and mus_musculus_core_45_36f should be used with the above ensembl branch 45 code.
</p>

<h3 class="boxed">Environment Variables</h3>

<p>
The following PERL5LIB environment variables should be set up:
</p>

<ul>

<li>
<p>
under <strong>tcsh/csh</strong> shell with:
</p>

<div class="example">
<pre>
setenv PERL5LIB ${PERL5LIB}:{HOME}/src/bioperl-live: \
${HOME}/src/ensembl/modules:${HOME}/src/ensembl-compara/modules
</pre>
</div>
</li>

<li>
<p>
- under <strong>bash</strong> shell with
</p>

<div class="example">
<pre>
export PERL5LIB=${PERL5LIB}:{HOME}/src/bioperl-live: \
${HOME}/src/ensembl/modules:${HOME}/src/ensembl-compara/modules
</pre>
</div>
</li>

</ul>

<p>
Bioperl and Ensembl must be in a directory called src set up in your home directory for this to work.
</p>


<h3 class="boxed">Connecting an Ensembl compara database</h3>

<p>
There are two ways to connect to the Ensembl Compara database. 
The old way uses the Bio::EnsEMBL::Compara::DBSQL::DBAdaptor explicitly. 
The new one uses the Bio::EnsEMBL::Registry module, which can read either a global or a specific configuration file or
auto-configure itself.
</p>

<h4>Implicitely, using the Bio::EnsEMBL::Registry auto-configuration feature (recommended)</h4>

<p>
For using the auto-configuration feature, you will only need to know about the host where the Ensembl databases are.
For instance, if you want to use the public Ensembl databases you can use the following command in your scripts:
</p>

<div class="example">
<pre>
use Bio::EnsEMBL::Registry;
Bio::EnsEMBL::Registry-&gt;load_registry_from_db(
    -host =&gt; 'ensembldb.ensembl.org',
    -user =&gt; 'anonymous',
    -port =&gt; 3306);
</pre>
</div>

<p>
Alternativelly, you can use a shorter version based on a URL:</p>

<div class="example">
<pre>
use Bio::EnsEMBL::Registry;
Bio::EnsEMBL::Registry-&gt;load_registry_from_url('mysql://anonymous@ensembldb.ensembl.org:3306/');
</pre>
</div>

<p>
This will create the DBAdaptors for all the Ensembl databases on that host. A set of common aliases is also provided
for convenience.
</p>

<h4>Implicitely, using the Bio::EnsEMBL::Registry configuration file</h4>

<p>
You will need to have a registry configuration file set up.
By default, it takes the file defined by the ENSEMBL_REGISTRY environment variable or
the file named .ensembl_init in your home directory if the former is not found.
Additionally, it is possible to use a specific file
(see perldoc Bio::EnsEMBL::Registry or later in this document for some examples on how to use a different file).
Please, refer to the <a href="../registry/index.html">Ensembl registry documentation</a> for details about this option.
</p>

<h4>Explicitely, using the Bio::EnsEMBL::Compara::DBSQL::DBAdaptor</h4>

<p>
Ensembl compara data as ensembl core data, is stored in a MySQL relational database. 
If you want to access a compara database, you will need to connect to it. 
This is done in exactly the same way as to connect to an ensembl core database, but using a Compara specific DBAdaptor.
</p>

<div class="example">
<pre>
use Bio::EnsEMBL::Compara::DBSQL::DBAdaptor

my $host = 'ensembldb.ensembl.org';
my $user = 'anonymous';
my $dbname = 'ensembl_compara_39';

my $comparadb= new Bio::EnsEMBL::Compara::DBSQL::DBAdaptor(
    -host	=&gt; $host,
    -user	=&gt; $user,
    -dbname =&gt; $dbname);
</pre>
</div>

<p>
As for a ensembl core connection, in addition to the parameters provided above, 
the optional port, driver and pass parameters can also be used to specify the TCP connection port, 
the type of database driver and the password respectively. 
These values have sensible defaults and can often be omitted.
</p>

<h3 class="boxed">Ensembl compara adaptors</h3>

<p>
Ensembl compara adaptors are used to fetch data from the database. Data are returned as Ensembl objects.
For instance, the GenomeDBAdaptor returns Bio::EnsEMBL::Compara::GenomeDB objects.
</p>

<p>
Below is a non exhaustive list of Ensembl compara adaptors that are most often used
</p>

<ul>
<li><p>GenomeDBAdaptor to fetch Bio::EnsEMBL::Compara::GenomeDB objects</p></li>
<li><p>DnaFragAdaptor to fetch Bio::EnsEMBL::Compara::DnaFrag objects</p></li>
<li><p>GenomicAlignBlockAdaptor to fetch Bio::EnsEMBL::Compara::GenomicAlignBlock objects</p></li>
<li><p>DnaAlignFeatureAdaptor to fetch Bio::EnsEMBL::DnaDnaAlignFeature objects
(note that this adaptor return a ensembl core object)</p></li>
<li><p>SyntenyAdaptor to fetch Bio::EnsEMBL::Compara::SyntenyRegion objects</p></li>
<li><p>MemberAdaptor to fetch Bio::EnsEMBL::Compara::Member objects</p></li>
<li><p>HomologyAdaptor to fetch Bio::EnsEMBL::Compara::Homology objects</p></li>
<li><p>FamilyAdaptor to fetch Bio::EnsEMBL::Compara::Family objects</p></li>
<li><p>PeptideAlignFeatureAdaptor to fetch Bio::EnsEMBL::Compara::PeptideAlignFeature objects</p></li>
</ul>

<p>
Only some of these adaptors will be used for illustration as part of this tutorial through commented perl scripts code.
</p>

<p>
You can get the adaptors from the Registry with the get_adaptor command. You need to specify 3 arguments: the
species name, the type of database and the type of object. Therefore, in order to get the GenomeDBAdaptor for the
compara database, you will need the following command:
</p>

<div class="example">
<pre>
my $genome_db_adaptor = Bio::EnsEMBL::Registry-&gt;get_adaptor(
    'Multi', 'compara', 'GenomeDB');
</pre>
</div>

<p>
<b>NB:</b> As the Ensembl compara DB is a multi-species database, the standard species name is 'Multi'. The type of the
database id 'compara'.

<h3 class="boxed">Code Conventions (and unconventions)</h3>

<p>
Refer to the Ensembl core tutorial for a good description of the coding conventions normally used in Ensembl. Please note that there may be exceptions to these rules in compara.
</p>

<p>
We can divide the fetching method of the ObjectAdaptors in two categories: the <b>fetch_by</b> and <b>fetch_all_by</b>.
The former return one single object while the latter return a reference to an array of objects.
</p>

<div class="example">
<pre>
my $this_genome_db = $genome_db_adaptor-&gt;fetch_by_name_assembly(&quot;Homo sapiens&quot;, &quot;NCBI36&quot;);
</pre>
</div>

<div class="example">
<pre>
my $all_genome_dbs = $genome_db_adaptor-&gt;fetch_all();
foreach my $this_genome_db (@{$all_genome_dbs}) {
  print $this_genome_db-&gt;name, &quot;\n&quot;;
}
</pre>
</div>

<h3 class="boxed">Whole Genome Alignments</h3>

<p>
The compara database contains a number of different types of whole genome alignments.
A listing about what are these different types can be found in the ensembl-compara/docs/schema_doc.html document in method_link section.
</p>

<h4>GenomicAlignBlock objects</h4>

<p>
GenomicAlignBlocks are the new way to store and fetch genomic alignments.
A GenomicAlignBlock contains several GenomicAlign objects.
Every GenomicAlign object corresponds to a piece of genomic sequence aligned with the other GenomicAlign in the same GenomicAlignBlock.
A GenomicAlign object is always related with other GenomicAlign objects and this relation is defined through the GenomicAlignBlock object.
Therefore the usual way to fetch genomic alignments is by fetching GenomicAlignBlock objects.
We have to start by getting the corresponding adaptor:
</p>

<div class="example">
<pre>
# Getting the GenomicAlignBlock adaptor:
my $genomic_align_block_adaptor = Bio::EnsEMBL::Registry-&gt;get_adaptor(
    'Multi', 'compara', 'GenomicAlign');
</pre>
</div>

<p>
In order to fetch the right alignments we need to specify a couple of data: 
the type of alignment and the piece of genomic sequence in which we are looking for alignments. 
The type of alignment is a more tricky now: 
you need to specify both the alignment method and the set of genomes. 
In order to simply this task, you could use the new Bio::EnsEMBL::Compara::MethodLinkSpeciesSet object. 
The best way to use them is by fetching them from the database:
</p>

<div class="example">
<pre>
# Getting the GenomeDB adaptor:
my $genome_db_adaptor = Bio::EnsEMBL::Registry-&gt;get_adaptor(
    'Multi', 'compara', 'GenomeDB');

# Fetching GenomeDB objects for human and mouse:
my $human_genome_db = $genome_db_adaptor-&gt;fetch_by_name_assembly('Homo sapiens');
my $mouse_genome_db = $genome_db_adaptor-&gt;fetch_by_name_assembly('Mus musculus');

# Getting the MethodLinkSpeciesSet adaptor:
my $method_link_species_set_adaptor = Bio::EnsEMBL::Registry-&gt;get_adaptor(
    'Multi', 'compara', 'MethodLinkSpeciesSet');

# Fetching the MethodLinkSpeciesSet object corresponding to BLASTZ_NET alignments between human and mouse genomic sequences:
my $human_mouse_blastz_net_mlss =
    $method_link_species_set_adaptor-&gt;fetch_by_method_link_type_GenomeDBs(
        &quot;BLASTZ_NET&quot;,
        [$human_genome_db, $mouse_genome_db]
    );
</pre>
</div>

<p>
There are two ways to fetch GenomicAlignBlocks. 
One is uses Bio::EnsEMBL::Slice objects while the second one is based on
Bio::EnsEMBL::Compara::DnaFrag objects for specifying the piece of genomic
sequence in which we are looking for alignments.
</p>

<div class="example">
<pre>
# Getting the Slice adaptor:
my $slice_adaptor = Bio::EnsEMBL::Registry-&gt;get_adaptor(
    $query_species, 'core', 'Slice');

# Fetching a Slice object:
my $query_slice = $qy_sa-&gt;fetch_by_region(
    'toplevel',
    $seq_region,
    $seq_region_start,
    $seq_region_end);

# Fetching all the GenomicAlignBlock corresponding to this Slice:
my $genomic_align_blocks =
    $genomic_align_block_adaptor-&gt;fetch_all_by_MethodLinkSpeciesSet_Slice(
      $human_mouse_blastz_net_mlss,
      $query_slice);
</pre>
</div>

<p>
Here is an example script with all of this:
</p>

<div class="example">
<pre>
use strict;
use Bio::EnsEMBL::Registry;
use Bio::EnsEMBL::Utils::Exception qw(throw);
use Bio::SimpleAlign;
use Bio::AlignIO;
use Bio::LocatableSeq;
use Getopt::Long;

my $usage = qq{
perl DumpMultiAlign.pl
  Getting help:
    [--help]

  For the query slice:
    [--species species]
        Query species. Default is &quot;human&quot;
    [--coord_system coordinates_name]
        Query coordinate system. Default is &quot;chromosome&quot;
    --seq_region region_name
        Query region name, i.e. the chromosome name
    --seq_region_start start
    --seq_region_end end

  For the alignments:
    [--alignment_type method_link_name]
        The type of alignment. Default is &quot;BLASTZ_NET&quot;
    [--set_of_species species1:species2:species3:...]
        The list of species used to get those alignments. Default is
        &quot;human:mouse&quot;. The names should correspond to the name of the
        core database in the registry_configuration_file or any of its
        aliases

  Ouput:
    [--output_format clustalw|fasta|...]
        The type of output you want. &quot;clustalw&quot; is the default.
    [--output_file filename]
        The name of the output file. By default the output is the
        standard output
};

my $species = &quot;human&quot;;
my $coord_system = &quot;chromosome&quot;;
my $seq_region = &quot;14&quot;;
my $seq_region_start = 75000000;
my $seq_region_end = 75010000;
my $alignment_type = &quot;BLASTZ_NET&quot;;
my $set_of_species = &quot;human:mouse&quot;;
my $output_file = undef;
my $output_format = &quot;clustalw&quot;;
my $help;

GetOptions(
    &quot;help&quot; =&gt; \$help,
    &quot;species=s&quot; =&gt; \$species,
    &quot;coord_system=s&quot; =&gt; \$coord_system,
    &quot;seq_region=s&quot; =&gt; \$seq_region,
    &quot;seq_region_start=i&quot; =&gt; \$seq_region_start,
    &quot;seq_region_end=i&quot; =&gt; \$seq_region_end,
    &quot;alignment_type=s&quot; =&gt; \$alignment_type,
    &quot;set_of_species=s&quot; =&gt; \$set_of_species,
    &quot;output_format=s&quot; =&gt; \$output_format,
    &quot;output_file=s&quot; =&gt; \$output_file);

# Print Help and exit
if ($help) {
    print $usage;
    exit(0);
}

if ($output_file) {
    open(STDOUT, &quot;&gt;$output_file&quot;) or die(&quot;Cannot open $output_file&quot;);
}

Bio::EnsEMBL::Registry-&gt;load_registry_from_db(
    -host =&gt; 'ensembldb.ensembl.org', -user =&gt; 'anonymous');

# Getting all the Bio::EnsEMBL::Compara::GenomeDB objects
my $genome_dbs;
my $genome_db_adaptor = Bio::EnsEMBL::Registry-&gt;get_adaptor(
    'Multi', 'compara', 'GenomeDB');

throw(&quot;Cannot connect to compara&quot;) if (!$genome_db_adaptor);

foreach my $this_species (split(&quot;:&quot;, $set_of_species)) {
    my $this_meta_container_adaptor = Bio::EnsEMBL::Registry-&gt;get_adaptor(
        $this_species, 'core', 'MetaContainer');

    throw(&quot;Registry configuration file has no data for connecting to &lt;$this_species&amp;&quot;)
        if (!$this_meta_container_adaptor);

    my $this_binomial_id = $this_meta_container_adaptor-&gt;get_Species-&gt;binomial;

    # Fetch Bio::EnsEMBL::Compara::GenomeDB object
    my $genome_db = $genome_db_adaptor-&gt;fetch_by_name_assembly($this_binomial_id);

    # Add Bio::EnsEMBL::Compara::GenomeDB object to the list
    push(@$genome_dbs, $genome_db);
}

# Getting Bio::EnsEMBL::Compara::MethodLinkSpeciesSet object
my $method_link_species_set_adaptor = Bio::EnsEMBL::Registry-&gt;get_adaptor(
    'Multi', 'compara', 'MethodLinkSpeciesSet');

my $method_link_species_set =
    $method_link_species_set_adaptor-&gt;fetch_by_method_link_type_GenomeDBs(
      $alignment_type, 
      $genome_dbs);

throw(&quot;The database do not contain any $alignment_type data for $set_of_species!&quot;)
    if (!$method_link_species_set);

# Fetching the query Slice:
my $slice_adaptor = Bio::EnsEMBL::Registry-&gt;get_adaptor($species, 'core', 'Slice');

throw(&quot;Registry configuration file has no data for connecting to &lt;$species&gt;&quot;)
    if (!$slice_adaptor);

my $query_slice = $slice_adaptor-&gt;fetch_by_region('toplevel', $seq_region, $seq_region_start, $seq_region_end);

throw(&quot;No Slice can be created with coordinates $seq_region:$seq_region_start-&quot;.
    &quot;$seq_region_end&quot;) if (!$query_slice);

# Fetching all the GenomicAlignBlock corresponding to this Slice:
my $genomic_align_block_adaptor = Bio::EnsEMBL::Registry-&gt;get_adaptor(
    'Multi', 'compara', 'GenomicAlignBlock');

my $genomic_align_blocks =
    $genomic_align_block_adaptor-&gt;fetch_all_by_MethodLinkSpeciesSet_Slice(
      $method_link_species_set, 
      $query_slice);

my $all_aligns;

# Get a Bio::SimpleAlign object from every GenomicAlignBlock
foreach my $this_genomic_align_block (@$genomic_align_blocks) {
    my $simple_align = $this_genomic_align_block-&gt;get_SimpleAlign;
    push(@$all_aligns, $simple_align);
}

# print all the genomic alignments using a Bio::AlignIO object
my $alignIO = Bio::AlignIO-&gt;newFh(
    -interleaved =&gt; 0,
    -fh =&gt; \*STDOUT,
    -format =&gt; $output_format,
    -idlength =&gt; 10
);
  
foreach my $this_align (@$all_aligns) {
    print $alignIO $this_align;
}

exit;
</pre>
</div>

<h3 class="boxed">Homologies and Protein clusters</h3>

<p>
All the homologies and families refer to Members. Homology objects store orthologous and paralogous relationships between Members and Family objects are clusters of Members.
</p>


<h4>Member objects</h4>

<p>
A Member represent either a gene or a protein. Most of them
are defined in the corresponding Ensembl core database. For
instance, the sequence for the human gene ENSG00000004059
is stored in the human core database.
</p>

<p>
The fetch_bt_source_stable_id method of the MemberAdaptor takes two arguments. The first one is the
source of the Member and can be:
</p>

<ul>
<li><p>ENSEMBLPEP, derived from an ensembl translation</p></li>
<li><p>ENSEMBLGENE, derived from an ensembl gene</p></li>
<li><p>SWISSPROT, derived from a Uniprot/Swissprot entry</p></li>
<li><p>SPTREMBL, derived from a Uniprot/SP-TrEMBL entry</p></li>
</ul>

<p>
The second argument is the identifier for the Member. Here is a simple example:
</p>

<div class="example">
<pre>
# get the MemberAdaptor
my $member_adaptor = Bio::EnsEMBL::Registry-&gt;get_adaptor(
    'Multi','compara','Member');

# fetch a Member
my $member = $member_adaptor-&gt;fetch_by_source_stable_id(
    'ENSEMBLGENE','ENSG00000004059');

# print out some information about the Member
print $member-&gt;chr_name, &quot; ( &quot;, $member-&gt;chr_start, &quot; - &quot;, $member-&gt;chr_end,
    &quot; ): &quot;, $member-&gt;description, &quot;\n&quot;;
</pre>
</div>

<p>
The Member object has several attributes:
</p>
<ul>
<li><p><b>source_name</b> and <b>stable_id</b> define this Member.</p></li>
<li><p><b>chr_name</b>, <b>chr_start</b>, <b>chr_end</b>, <b>chr_strand</b> locate this Member on the genome but
are only available for ENSEMBLGENE and ENSEMBLPEP.</p></li>
<li><p><b>taxon_id</b> corresponds to the NCBI taxonomy identifier (see
<a href="http://www.ncbi.nlm.nih.gov/Taxonomy/taxonomyhome.html/">http://www.ncbi.nlm.nih.gov/Taxonomy/taxonomyhome.html/</a>
for more details).</p></li>
<li><p><b>taxon</b> returns a Bio::EnsEMBL::Compara::Taxon object that inherits itself from Bio::Species,
so from this object you can get additional information about the species.</p></li>
</ul>

<div class="example">
<pre>
my $taxon = $member-&gt;taxon;
print join &quot;; &quot;, map { $taxon-&gt;$_ } qw(common_name genus species binomial classification),&quot;\n&quot;;
</pre>
</div>

<p>
respectively for these method calls and in the case of human species, you will obtain
human; Homo; sapiens; Homo sapiens; sapiens Homo Hominidae Catarrhini Primates 
Eutheria Mammalia Euteleostomi Vertebrata Craniata Chordata Metazoa Eukaryota
</p>


<h4>Homology Objects</h4>

<p>
A Homology object represents either an orthologous or paralogous relationships between two
or more Members.
</p>

<p>
Typically you want to get homologies for a given gene. The HomologyAdaptor has a
fetching method called fetch_all_by_Member(). You will need the Member object for your
query gene, therefore you will fetch the Member first like in this example:
</p>

<div class="example">
<pre>
# first you have to get a Member object. In case of homology is a gene, in 
# case of family it can be a gene or a protein

my $member_adaptor = Bio::EnsEMBL::Registry-&gt;get_adaptor('Multi', 'compara', 'Member');
my $member = $member_adaptor-&gt;fetch_by_source_stable_id('ENSEMBLGENE','ENSG00000004059');

# then you get the homologies where the member is involved

my $homology_adaptor = Bio::EnsEMBL::Registry-&gt;get_adaptor('Multi', 'compara', 'Homology');
my $homologies = $homology_adaptor-&gt;fetch_all_by_Member($member);

# That will return a reference to an array with all homologies (orthologues in
# other species and paralogues in the same one)
# Then for each homology, you can get all the Members implicated

foreach my $homology (@{$homologies}) {
  # You will find different kind of description
  # UBRH, MBRH, RHS, YoungParalogues
  # see ensembl-compara/docs/docs/schema_doc.html for more details

  print $homology-&gt;description,&quot; &quot;, $homology-&gt;subtype,&quot;\n&quot;;

  # And if they are defined dN and dS related values

  print join &quot; &quot;, map { $homology-&gt;$_ } qw(dn ds n s lnl threshold_on_ds),&quot;\n&quot;;

}
</pre>
</div>

<p>
Each homology relation has 2 or more members, you should find there the initial member used as a query.
The get_all_MemberAttribute method returns an array of pairs of Member and Attributes. The Member
corresponds to the gene or protein and the Attribute object contains information about how this
Member has been aligned.
</p>

<div class="example">
<pre>
foreach my $member_attribute (@{$homology-&gt;get_all_Member_Attribute})

  # for each Member, you get information on the Member specifically and in
  # relation to the homology relation via Attribute object

  my ($member, $attribute) = @{$member_attribute};
  print join &quot; &quot;, map { $member-&gt;$_ }  qw(stable_id taxon_id),&quot;\n&quot;;
  print join &quot; &quot;, map { $attribute-&gt;$_ } qw(perc_id perc_pos perc_cov),&quot;\n&quot;;

}
</pre>
</div>

<p>
You can get the original alignment used to define an homology:
</p>

<div class="example">
<pre>
my $simple_align = $homology-&gt;get_SimpleAlign();
my $alignIO = Bio::AlignIO-&gt;newFh(
    -interleaved =&gt; 0,
    -fh =&gt; \*STDOUT,
    -format =&gt; &quot;clustalw&quot;,
    -idlength =&gt; 20);

print $alignIO $simple_align;
</pre>
</div>

<h4>Family Objects</h4>

<p>
Families are clusters of peptides including all the Ensembl peptides plus all the
metazoan SwissProt and SP-Trembl entries. The object and the adaptor are really
similar to the previous ones.
</p>

<div class="example">
<pre>
my $member_adaptor = Bio::EnsEMBL::Registry-&gt;get_adaptor($dbname,'compara','Member');
my $member = $member_adaptor-&gt;fetch_by_source_stable_id('ENSEMBLGENE','ENSG00000004059');

my $family_adaptor = Bio::EnsEMBL::Registry-&gt;get_adaptor('Multi','compara','Family');
my $families = $family_adaptor-&gt;fetch_all_by_Member($member);

foreach my $family (@{$families}) {
    print join &quot; &quot;, map { $family-&gt;$_ }  qw(description description_score),&quot;\n&quot;;

    foreach my $member_attribute (@{$family-&gt;get_all_Member_Attribute}) {
        my ($member, $attribute) = @{$member_attribute};
        print $member-&gt;stable_id,&quot; &quot;,$member-&gt;taxon_id,&quot;\n&quot;;
    }

    my $simple_align = $family-&gt;get_SimpleAlign();
    my $alignIO = Bio::AlignIO-&gt;newFh(
        -interleaved =&gt; 0,
        -fh =&gt; \*STDOUT,
        -format =&gt; &quot;phylip&quot;,
        -idlength =&gt; 20);

    print $alignIO $simple_align;

    $simple_align = $family-&gt;get_SimpleAlign('cdna');
    my $alignIO = Bio::AlignIO-&gt;newFh(
        -interleaved =&gt; 0,
        -fh =&gt; \*STDOUT,
        -format =&gt; &quot;phylip&quot;,
        -idlength =&gt; 20);

    print $alignIO $simple_align;
}
</pre>
</div>


<h3 class="boxed">Further help</h3>

<p>
For additional information or help mail the <a href="mailto:ensemb-dev&#64;ebi.ac.uk">ensembl-dev</a> mailing list.
You will need to subscribe to this mailing list to use it.
More information on subscribing to any Ensembl mailing list is available from the 
<a href="../../about/contact.html">Ensembl Contacts</a> page.
</p>

</body>
</html>
